buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.github.jruby-gradle:jruby-gradle-plugin:0.4.0'
        classpath 'com.github.jruby-gradle:jruby-gradle-jar-plugin:0.4.0'
    }
}

apply plugin: 'java'
apply plugin: 'com.github.jruby-gradle.base'
apply plugin: 'com.github.jruby-gradle.jar'
import com.github.jrubygradle.JRubyExec

configurations {
    compile.extendsFrom jrubyJar
}

dependencies {
    jrubyExec 'rubygems:rspec:2.99.0+' // <1>
    gems 'rubygems:colorize:0.7.7' // <2>
    jrubyJar 'org.slf4j:slf4j-simple:1.7.12' // <3>
}

task spec(type: JRubyExec) {
    group 'Verification' 
    description 'Run the RSpecs for this project'
    dependsOn classes // <1>
    classpath sourceSets.main.runtimeClasspath // <2>
    jrubyArgs '-S' // <3>
    script 'rspec' // <4>
    scriptArgs '--order', 'random', '--color' // <5>
}
/* let's make our top-level check task run RSpec too */
check.dependsOn spec

jrubyJar {
    from sourceSets.main.output // <1>
    initScript 'main.rb' // <2>
    defaultGems() // <3>
    /* no sense building a jar without passing tests */
    dependsOn spec
}
assemble.dependsOn check
assemble.dependsOn jrubyJar

/* cute hack to disable packaging our java up as a library-type jar */
jar.enabled = false
